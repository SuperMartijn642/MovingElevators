plugins {
    id "eclipse"
    id "idea"
    id "java-library"
    id "net.neoforged.gradle.userdev" version "7.0.154"
    id "me.modmuss50.mod-publish-plugin" version "0.5.2"
}

version = mod_version + "-neoforge-" + minecraft_suffix
group = maven_group
base.archivesName = mod_id

java.toolchain.languageVersion = JavaLanguageVersion.of(java_target)

println("Java: " + System.getProperty("java.version") + " JVM: " + System.getProperty("java.vm.version") + "(" + System.getProperty("java.vendor") + ") Arch: " + System.getProperty("os.arch"))

// Keep parameter names when compiling
compileJava.options.compilerArgs.add '-parameters'

repositories {
    flatDir { dirs "libs" }
    maven { url "https://www.cursemaven.com" }
    exclusiveContent {
        forRepository {
            maven { url = "https://api.modrinth.com/maven" }
        }
        filter { includeGroup "maven.modrinth" }
    }
    maven { url = "https://repo.spongepowered.org/maven" }
}

dependencies {
    // NeoForge
    implementation "net.neoforged:neoforge:${neoforge_version}"

    // Core library
    implementation "curse.maven:supermartijn642s-core-lib-454372:${core_library_file}"
    // Config library
    implementation "curse.maven:supermartijn642s-config-lib-438332:${config_library_file}"

    // Embeddium
    implementation "curse.maven:embeddium-908741:${embeddium_file}"

    // Just Enough Items
//    runtimeOnly "curse.maven:jei-238222:${just_enough_items_file}"
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir "src/generated/resources" }

tasks.withType(ProcessResources).configureEach {
    inputs.property "version", version

    // Replace the mod version
    Map<String, ?> copyProperties = project.properties.clone() as Map<String, ?>
    //noinspection UnnecessaryQualifiedReference
    var matcher = java.util.regex.Pattern.compile("[^.0-9]").matcher(copyProperties.mod_version as String)
    if (matcher.find())
        copyProperties.mod_version = copyProperties.mod_version.substring(0, matcher.start()) + "+" + copyProperties.mod_version.substring(matcher.start())

    filesMatching(["META-INF/mods.toml", "modid.mixins.json", "pack.mcmeta"]) {
        expand copyProperties
    }

    exclude "**/*.pdn"

    rename "^modid.mixins.json\$", "${mod_id}.mixins.json"
    rename "^icon.png\$", "${mod_id}.png"
}

minecraft.accessTransformers.file rootProject.file("src/main/resources/META-INF/accesstransformer.cfg")

runs {
    configureEach {
        systemProperty "forge.logging.markers", "REGISTRIES"

        //noinspection GroovyAssignabilityCheck
        modSource sourceSets.main
    }

    client {
    }

    server {
        programArgument "-nogui"
    }

    data {
        //noinspection GroovyAssignabilityCheck
        programArguments "--mod", mod_id
        programArguments "--all"
        programArguments "--output", file("src/generated/resources/").getAbsolutePath()
        programArguments "--existing", file("src/main/resources").getAbsolutePath()
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

// Force the jar to be reobfuscated
jar.finalizedBy('reobfJar')

publishMods {
    file = jar.archiveFile
    displayName = "${mod_name} ${mod_version} for NeoForge ${minecraft_suffix.substring(2)}"
    version = project.version
    //noinspection UnnecessaryQualifiedReference
    type = me.modmuss50.mpp.ReleaseType.of(publishing_release_type.toUpperCase())
    changelog = file("changelog.md").text
    modLoaders.add("neoforge")
    maxRetries = 2

    curseforge {
        accessToken = System.getenv("CURSEFORGE_TOKEN")
        projectId = curseforge_project_id
        if (!curseforge_required_dependency_ids.isEmpty())
            curseforge_required_dependency_ids.split(" ").each it::requires
        if (!curseforge_optional_dependency_ids.isEmpty())
            curseforge_optional_dependency_ids.split(" ").each it::optional
        publishing_game_versions.split(" ").each minecraftVersions::add
        clientRequired = true
        serverRequired = true
    }

    modrinth {
        accessToken = System.getenv("MODRINTH_TOKEN")
        projectId = modrinth_project_id
        displayName = "${mod_name} ${mod_version}"
        if (!modrinth_required_dependency_ids.isEmpty())
            modrinth_required_dependency_ids.split(" ").each it::requires
        if (!modrinth_optional_dependency_ids.isEmpty())
            modrinth_optional_dependency_ids.split(" ").each it::optional
        publishing_game_versions.split(" ").each minecraftVersions::add
    }
}

// Rename the 'publishMods' task
tasks.publishMods.group = "other"
tasks.register("publishAll") {
    group = "publishing"
    dependsOn(tasks.publishMods)
}
