buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '4.1.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7.+'
    }
}
apply plugin: 'net.minecraftforge.gradle'
// Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
apply plugin: 'eclipse'
apply plugin: 'org.spongepowered.mixin'

version = '1.3.1-mc1.16'
group = 'com.supermartijn642.movingelevators' // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = 'movingelevators'

java.toolchain.languageVersion = JavaLanguageVersion.of(8) // Mojang ships Java 8 to end users, so your mod should target Java 8.

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))

minecraft {
    mappings channel: 'official', version: '1.16.5'
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.

    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'

            mods {
                movingelevators {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'

            mods {
                movingelevators {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'

            args '--mod', 'movingelevators', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                movingelevators {
                    source sourceSets.main
                }
            }
        }
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven {
        url "https://www.cursemaven.com"
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.16.5-36.2.0'

    // Core lib
    compile fg.deobf('curse.maven:supermartijn642s-core-lib-454372:3596490')
    // Config lib
    compile fg.deobf('curse.maven:supermartijn642s-config-lib-438332:3413938')

    // Block Renderer
    runtimeOnly fg.deobf("curse.maven:block-renderer-380456:3294692")
    // Just Enough Items
    runtimeOnly fg.deobf('curse.maven:jei-238222:3345971')
}

mixin {
    disableAnnotationProcessorCheck()
}